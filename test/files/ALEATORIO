/*//#########################################################################################
Projeto : POUPEX
Modulo  : SIGAATF
Fonte   : PXATFR02
Objetivo: Relatório de Conferência do Ativo Fixo.
*///#########################################################################################

#INCLUDE 'TOTVS.CH'
#INCLUDE "TbiConn.CH"
#DEFINE REL_NAME "PXATFR02"

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} PXATFR02
Função inicial
@author    Eduardo Santana
@version   1.0
@since     16/12/2016
/*/
//------------------------------------------------------------------------------------------
User Function PXATFR02()
	Private oPrint := Nil
	Private lLogin := Type("cEmpAnt") != "U"

	If !lLogin
		PREPARE ENVIRONMENT EMPRESA "02" FILIAL "02DF0001"
		MV_PAR01 := "07/2017"
	EndIf

	ManualPrint()
Return

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} ManualPrint
Descrição
Verifica o diretorio e faz a pergunta de parametros ao usuario
@author    Eduardo Santana
@version   1.00
@since     16/12/2016
/*/
//------------------------------------------------------------------------------------------
Static Function ManualPrint()
	Local cDir 		:=  GetTempPath(.T.) + "totvsprinter\"
	Local dLastDay, dDataA
	Local cMensagem := ""
	Local aErros	:= {}
	Private lPendFechamento
	Private cPerg 	:= "PXATFR02"
	Private oSay 	:= nil

	If !ExistDir(cDir)
		If MakeDir(cDir) <> 0
			Alert("Não foi possivel criar o diretorio " + cDir + " Verifique se o diretorio existe ou se o sistema tem permissão para criar o diretorio e tente novamente.")
		EndIf
	EndIf

	If lLogin
		If !ValidPerg(cPerg)
			Return
		EndIf
	EndIf

	//Valida se todas as filiais foram fechadas
	dLastDay := LastDay(cToD("01/" + MV_PAR01))

	dbSelectArea("SX6")
	SET FILTER TO AllTrim(X6_VAR) == "MV_ULTDEPR"
	SX6->(dbGotop())
	While ! SX6->(Eof())
		If STOD(SX6->X6_CONTEUD) < dLastDay .AND. ! empty(SX6->X6_FIL)
			aAdd(aErros,{STOD(SX6->X6_CONTEUD),SX6->X6_FIL})
		EndIf
		SX6->(DbSkip())
	EndDo

	SET FILTER TO

	aErros := ASORT(aErros, , , { | x,y | x[2] < y[2] } )
	dDataA := sTod("")
	For nI := 1 to Len(aErros)
		If dDataA != aErros[nI][1]
			cMensagem += "<br>Ultimo Fechamento em: " + dToc(aErros[nI][1]) +"<p>"
			cMensagem += " Filial(is) com fechamento pendente:<p>"
			dDataA 	  := aErros[nI][1]
		EndIf
		If Mod(nI,10) == 0
			cMensagem += "<p>"
		EndIf
		cMensagem += aErros[nI][2] + ","
	Next nI

	lPendFechamento := ! Empty(cMensagem)
	If lPendFechamento
		cMensagem := SubStr(cMensagem,1,len(cMensagem)-1) + "!"
		MessageBox(cMensagem,"FECHAMENTO PENDENTE EM FILIAL(IS)!",16)
		Return
	EndIf

	If lLogin
		FwMsgRun( ,{ |oSay| PaintPrint(cDir,, oSay) })
	Else
		MsgRun("Preparando dados para o relatório...","Processando...", { || PaintPrint(cDir,, oSay) } )
	EndIf
Return

//------------------------------------------------------------------------------------------
/*/{Protheus.doc}PaintPrint
Descrição
Prepara os dados para a impressao
@author    Eduardo Santana
@version   1.00
@since     16/12/2016
/*/
//------------------------------------------------------------------------------------------

Static Function PaintPrint(cPathDest,cRelName, oSay)
	Local lView		:= .T.
	Local cAlias := GetNextAlias()
	Local cMsg := ""
	Default cRelName:= REL_NAME+"_"+Substring(StrTran(Time(),":",""),1,4)+cValtoChar(Randomize(1,999999))
	Private aDados := {}
	Private nLin := 65
	Private nV := 16
	Private nPag := 1
	Private aDados := {}

	aDados := BuscaDados()

	//Instancia a Classe FwMsPrinter
	oPrint := FwMsPrinter():New(cRelName,6,.F.,cPathDest,.T./*lAuto*/,.F.,@oPrint,,,.F.,.F.,lView,/*nQtdPags*/)
	oPrint:SetResolution(72)
	oPrint:SetLandscape()
	oPrint:SetPaperSize(DMPAPER_A4)
	oPrint:SetMargin(05,05,05,05) 		//nEsquerda, nSuperior, nDireita, nInferior
	oPrint:cPathPDF := cPathDest 		//Caso seja utilizada impressão em IMP_PDF

	PaintRel(oSay)

	oPrint:Print()
Return


//------------------------------------------------------------------------------------------
/*/{Protheus.doc}PaintRel
Descrição
Realiza a pintura dos objetos no relatorio
@author    Eduardo Santana
@version   1.00
@since     16/12/2016

/*/
//------------------------------------------------------------------------------------------
Static Function PaintRel(oSay)
	Local nTotalGeral := 0
	Local cTipoPgto := ""
	Local oMainFont := u_xFonte(10,,,,"Lucida")
	Local oHeaderFont := u_xFonte(14,,,,"Lucida")
	Local oHdrBld := u_xFonte(14,.T.,,,"Lucida")
	Local oMainBold := u_xFonte(10,.T.,,,"Lucida")
	Local nVlrResid := 0
	Local nTotAtvSi := 0
	Local nTotAtvAq := 0
	Local nTotAtvMv := 0
	Local nTotAtvTf := 0
	Local nTotAtvBx := 0
	Local nTotDprSi := 0
	Local nTotDprAq := 0
	Local nTotDprMv := 0
	Local nTotDprTf := 0
	Local nTotDprBx := 0
	Local nAtvSldFnl := 0
	Local nDprSldFnl := 0
	Local nTotAtvSd := 0
	Local nTotDprSd := 0
	Local nTotResid := 0
	Private nPag := 1


	If lLogin
		oSay:cCaption  := "Imprimindo Dados..."
	EndIf

		oPrint:StartPage()

	oPrint:SayBitmap(30,50,"\system\LOGO" + cEmpAnt + ".png",130,40)

	oPrint:Say(nLin, 182, "Sistema de Gestão Patrimonial",oHeaderFont,,,)
	oPrint:Line(nLin+4, 182, nLin+4  , 815,, "-9"  )

	nLin += nV*2

	oPrint:SayAlign( nLin,30, "PXATFR02 / CONSOLIDADO POR EMPRESA" /*+ FWFilialName()*/,oHeaderFont,250, 50, , 0, 0 )
	oPrint:SayAlign( nLin,30, "EVOLUÇÃO DO ATIVO POR PERIODO",oHdrBld,815, 50, , 2, 0 )
	oPrint:SayAlign( nLin,0, "Período: " + MV_PAR01,oHeaderFont,815, 50, , 1, 0 )

	nLin += nV

	oPrint:SayAlign( nLin,30, "FISCAL - Moeda corrente",oHeaderFont,815, 50, , 2, 0 )

	oPrint:Line(nLin,20,nLin,815,,"-9")

	nV := 10
	nLin += nV*2

		oPrint:SayAlign( nLin,30, "Grupo Ativo",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,90, "Código contábil",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,190, "Saldo Inicial",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,280, "Aquis/Deprec",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,370, "Movimentações",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,460, "Transferências",oMainFont,590, 100, , 0, 0 )
		oPrint:SayAlign( nLin,550, "Baixas",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,640, "Saldo Final",oMainFont,590, 50, , 0, 0 )
		oPrint:SayAlign( nLin,730, "Residual",oMainFont,590, 50, , 0, 0 )


	nLin += nV*2

	oPrint:Line(nLin,20,nLin,815,,"-9")

	For nI := 1 To Len(aDados)

		oPrint:SayAlign( nLin,30,AllTrim(aDados[nI][1]),oMainFont,400, 100, , 0, 0 )
		nLin += nV

		nAtvSldFnl := (aDados[nI][4]+aDados[nI][5]+aDados[nI][6])-aDados[nI][8]
		nDprSldFnl := (aDados[nI][9]+aDados[nI][10]+aDados[nI][11])-aDados[nI][13]
		nVlrResid := nAtvSldFnl - nDprSldFnl

		oPrint:SayAlign( nLin,80,"Ativo : ",oMainBold,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,120 ,aDados[nI][2],oMainFont,150, 50, , 0, 0 )
		oPrint:SayAlign( nLin,180 ,Transform(aDados[nI][4], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,270 ,Transform(aDados[nI][5], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,360 ,Transform(aDados[nI][6], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,450 ,Transform(aDados[nI][7], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,540 ,Transform(aDados[nI][8], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,630 ,Transform(nAtvSldFnl, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,720 ,Transform(nVlrResid, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )

		nTotAtvSi += aDados[nI][4]
		nTotAtvAq += aDados[nI][5]
		nTotAtvMv += aDados[nI][6]
		nTotAtvTf += aDados[nI][7]
		nTotAtvBx += aDados[nI][8]


		nLin += nV
		oPrint:SayAlign( nLin,80,"Deprec: ",oMainBold,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,120 ,aDados[nI][3],oMainFont,150, 50, , 0, 0 )
		oPrint:SayAlign( nLin,180 ,Transform(aDados[nI][9], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,270 ,Transform(aDados[nI][10], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,360 ,Transform(aDados[nI][11], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,450 ,Transform(aDados[nI][12], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,540 ,Transform(aDados[nI][13], "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		oPrint:SayAlign( nLin,630 ,Transform(nDprSldFnl, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
		nLin += nV * 1.5

		nTotDprSi += aDados[nI][9]
		nTotDprAq += aDados[nI][10]
		nTotDprMv += aDados[nI][11]
		nTotDprTf += aDados[nI][12]
		nTotDprBx += aDados[nI][13]

		nTotAtvSd += nAtvSldFnl
		nTotDprSd += nDprSldFnl
		nTotResid += nVlrResid

	Next nI

	nLin += nV

	oPrint:Line(nLin,20,nLin,815,,"-9")

	oPrint:SayAlign( nLin,30,'Total Tipo Contabil:',oMainBold,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,180,Transform(nTotAtvSi, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,270 ,Transform(nTotAtvAq, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,360 ,Transform(nTotAtvMv, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,450 ,Transform(nTotAtvTf, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,540 ,Transform(nTotAtvBx, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,630 ,Transform(nTotAtvSd, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,720 ,Transform(nTotResid, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )

	nLin += nV

	oPrint:SayAlign( nLin,180,Transform(nTotDprSi, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,270 ,Transform(nTotDprAq, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,360 ,Transform(nTotDprMv, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,450 ,Transform(nTotDprTf, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,540 ,Transform(nTotDprBx, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )
	oPrint:SayAlign( nLin,630 ,Transform(nTotDprSd, "@E 999,999,999,999.99" ),oMainFont,150, 100, , 0, 0 )

	nLin += nV

	oPrint:SayAlign( 580,30,"Emitido em : "+ DTOC(Date()) + " - " +  Time() + If(lPendFechamento," com pendencias de fechamento",""),oMainBold,400, 50, , 0, 0 )

	oPrint:Say( 585 ,500,U_InspFonte("PXATFR02.PRW"),u_xFonte(8))

	oPrint:EndPage()
Return

//------------------------------------------------------------------------------------------
/*/{Protheus.doc}BuscaDados
Descrição
Busca as informações no banco de dados
@author    Eduardo Santana
@version   1.00
@since     16/12/2016
/*/
//------------------------------------------------------------------------------------------

Static Function BuscaDados()
	Local TMP := GetNextAlias()
	Local aDados := {}
	Local cLastDayBef := DTOS(CTOD("01/" + MV_PAR01)-1)
	Local cLastDay := DTOS(LastDay(cToD("01/" + MV_PAR01)))
	Local cFirstDay := DTOS(CTOD("01/" + MV_PAR01))
	Local cUltDepr := DTOS(GetMv("MV_ULTDEPR"))

	BeginSql Alias TMP

	SELECT
		NG_DESCRIC, // Grupo Ativo
		N3_CCONTAB, // Código Contábil
		N3_CDEPREC, // Conta de depreciação
		SUM(CASE WHEN ID = 1 THEN ATIVO ELSE 0 END) SI_ATIVO, // SALDO INICIAL ATIVO
		SUM(CASE WHEN ID = 1 THEN DEPRECIACAO ELSE 0 END) SI_DEPREC, //SALDO INICAL DEPRECIAÇÃO
		SUM(CASE WHEN ID = 2 THEN ATIVO ELSE 0 END) MV_ATIVO, //MOVIMENTO ATIVO
		SUM(CASE WHEN ID = 2 THEN DEPRECIACAO ELSE 0 END) MV_DEPREC, //MOVIMENTO DEPRECIAÇÃO
		SUM(CASE WHEN ID = 3 THEN ATIVO ELSE 0 END) BX_ATIVO, //BAIXA ATIVO
		SUM(CASE WHEN ID = 3 THEN DPRACUM ELSE 0 END) BX_DEPREC, //BAIXA DEPRECIAÇÃO
		SUM(CASE WHEN ID = 4 THEN ATIVO ELSE 0 END) TF_ATIVO, //TRANSFWERÊNCIA ATIVO
		SUM(CASE WHEN ID = 4 THEN DPRACUM ELSE 0 END) TF_DEPREC, //TRANSFWERÊNCIA ATIVO
		SUM(CASE WHEN ID = 5 THEN ATIVO ELSE 0 END) AQ_ATIVO,  //AQUISIÇÕES ATIVO
		SUM(CASE WHEN ID = 6 THEN DPRACUM ELSE 0 END) AQ_DEPREC //AQUISIÇÕES DEPRECIAÇÃO
	FROM (
	SELECT ID,
		N1_GRUPO,
		NG_DESCRIC,
		(VORIG + AMPLIA)  ATIVO,
	CASE
			WHEN ID='2' THEN SLDINIDEPR
	ELSE VORIG - SLDINIDEPR
	END DEPRECIACAO,
		DPRACUM,
		N3_CCONTAB,
		N3_CDEPREC
	FROM (
		SELECT CASE WHEN SN4.N4_DATA <= %exp:cLastDayBef%  THEN '1' ELSE '2' END ID,
			SNG.NG_GRUPO N1_GRUPO,
			NG_DESCRIC,
               SUM(CASE
                     WHEN (SN4.N4_OCORR IN ('05', '04','09') AND SN4.N4_TIPOCNT = '1' AND SN4.N4_DATA <= %exp:cLastDayBef%)
                          OR (SN4.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay% AND GPFIN1.GRUPOPR IS NOT NULL AND SN4.N4_OCORR = '04' AND SN4.N4_TIPOCNT = '1' )
					 THEN SN4.N4_VLROC1
                     WHEN (SN4.N4_OCORR IN ('03') AND SN4.N4_TIPOCNT = '1' AND SN4.N4_DATA <= %exp:cLastDayBef%)
                          OR (SN4.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay% AND GPFIN1.GRUPOPR IS NOT NULL AND SN4.N4_OCORR = '03' AND SN4.N4_TIPOCNT = '1' )
					 THEN -SN4.N4_VLROC1
ELSE 0 END) VORIG,
			0 DPRACUM,
             SUM (CASE
                     WHEN
					 	 (SN4.N4_DATA <= %exp:cLastDayBef% AND
                         ((SN4.N4_OCORR = '05' AND SN4.N4_TIPOCNT = '1')
                          OR (SN4.N4_OCORR = '01' AND SN4.N4_TIPOCNT = '4')
                          OR (SN4.N4_OCORR = '09' AND SN4.N4_TIPOCNT = '1')
                          OR (SN4.N4_OCORR = '04' AND SN4.N4_TIPOCNT = '1' AND SN4.N4_GRUPOTR=' ')
                          OR (SN4.N4_OCORR = '03' AND SN4.N4_TIPOCNT = '1' AND SN4.N4_GRUPOTR<>' ')
                          OR (SN4.N4_OCORR = '04' AND SN4.N4_TIPOCNT = '4' AND SN4.N4_GRUPOTR<>' ') ))
						  OR (SN4.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay% AND GPFIN1.GRUPOPR IS NOT NULL AND SN4.N4_GRUPOTR<>' ' AND SN4.N4_OCORR = '04' AND SN4.N4_TIPOCNT = '4' )
                      THEN SN4.N4_VLROC1
                      WHEN SN4.N4_DATA <= %exp:cLastDayBef% OR (SN4.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay% AND GPFIN1.GRUPOPR IS NOT NULL AND SN4.N4_GRUPOTR<>' ' AND SN4.N4_OCORR = '03' AND SN4.N4_TIPOCNT = '4' )
                      THEN - SN4.N4_VLROC1
ELSE 0
END) SLDINIDEPR,
			  0  AMPLIA,
CASE WHEN GPINI.CONTADE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.CONTADE
                           WHEN GPFIN1.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN1.CONTADE
                           WHEN GPFIN2.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB END N3_CCONTAB,
			 N3_CDEPREC
		FROM %table:SN3% SN3
		INNER JOIN %table:SN1% SN1 ON
				SN1.%NotDel%
			AND SN1.N1_FILIAL = SN3.N3_FILIAL
			AND SN1.N1_CBASE = SN3.N3_CBASE
			AND SN1.N1_ITEM = SN3.N3_ITEM
			AND (N1_BAIXA >= %exp:cFirstDay% OR N1_BAIXA = ' ')
		INNER JOIN %table:SN4% SN4 ON
				SN4.%NotDel% AND N3_CBASE = SN4.N4_CBASE
			AND N3_FILIAL = SN4.N4_FILIAL AND N3_ITEM = SN4.N4_ITEM
			AND N3_TIPO = SN4.N4_TIPO
			AND SN4.N4_DATA <= %exp:cLastDay%
			AND SN4.N4_TIPOCNT IN ('1','4')
		// TRANF GROPO INICIO DO PERIODO
		LEFT JOIN
				(SELECT
				MIN(N4GPI.N4_DATA) N4_DATA,
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR GRUPODE, SNMGPI.NM_GRUPO GRUPOPR, N4GPI.N4_CONTA CONTADE, SNMGPI.NM_CCONTAB CONTAPR
				FROM %table:SNM% SNMGPI
				JOIN %table:SN4% N4GPI ON
						N4GPI.D_E_L_E_T_= ' '
					AND N4GPI.N4_FILIAL = SNMGPI.NM_FILIAL
					AND N4GPI.N4_CBASE = SNMGPI.NM_CBASE
					AND N4GPI.N4_ITEM = SNMGPI.NM_ITEM
					AND N4GPI.N4_DATA = SNMGPI.NM_DTAPVRE
					AND N4GPI.N4_OCORR = '03'
					AND N4GPI.N4_TIPOCNT = '1'
					AND N4GPI.N4_GRUPOTR <> SNMGPI.NM_GRUPO
					AND N4GPI.N4_GRUPOTR <> ' '
				WHERE
						SNMGPI.D_E_L_E_T_= ' '
					AND SNMGPI.NM_MOTBX=' '
				GROUP BY
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR, SNMGPI.NM_GRUPO, N4GPI.N4_CONTA, SNMGPI.NM_CCONTAB  ) GPINI ON
				GPINI.N4_FILIAL = SN3.N3_FILIAL
			AND GPINI.N4_CBASE = SN3.N3_CBASE
			AND GPINI.N4_ITEM = SN3.N3_ITEM
			AND GPINI.N4_DATA >= %exp:cFirstDay%
		// TRANF GROPO FINAL DO PERIODO
		LEFT JOIN
				(SELECT
				MAX(N4GPF1.N4_DATA) N4_DATA,
				N4GPF1.N4_FILIAL, N4GPF1.N4_CBASE, N4GPF1.N4_ITEM, N4GPF1.N4_TIPO, N4GPF1.N4_GRUPOTR GRUPODE, SNMGPF1.NM_GRUPO GRUPOPR, N4GPF1.N4_CONTA CONTADE, SNMGPF1.NM_CCONTAB CONTAPR
				FROM %table:SNM% SNMGPF1
				JOIN %table:SN4% N4GPF1 ON
						N4GPF1.D_E_L_E_T_= ' '
					AND N4GPF1.N4_FILIAL = SNMGPF1.NM_FILIAL
					AND N4GPF1.N4_CBASE = SNMGPF1.NM_CBASE
					AND N4GPF1.N4_ITEM = SNMGPF1.NM_ITEM
					AND N4GPF1.N4_DATA = SNMGPF1.NM_DTAPVRE
					AND N4GPF1.N4_OCORR = '03'
					AND N4GPF1.N4_TIPOCNT = '1'
					AND N4GPF1.N4_GRUPOTR <> SNMGPF1.NM_GRUPO
					AND N4GPF1.N4_GRUPOTR <> ' '
				WHERE
						SNMGPF1.D_E_L_E_T_= ' '
					AND SNMGPF1.NM_MOTBX=' '
				GROUP BY
				N4GPF1.N4_FILIAL, N4GPF1.N4_CBASE, N4GPF1.N4_ITEM, N4GPF1.N4_TIPO, N4GPF1.N4_GRUPOTR, SNMGPF1.NM_GRUPO, N4GPF1.N4_CONTA, SNMGPF1.NM_CCONTAB  ) GPFIN1 ON
				GPFIN1.N4_FILIAL = SN3.N3_FILIAL
			AND GPFIN1.N4_CBASE = SN3.N3_CBASE
			AND GPFIN1.N4_ITEM = SN3.N3_ITEM
			AND GPFIN1.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay%
		// TRANF GROPO INICAL DO PERIODO SEQUINTE
		LEFT JOIN
			(SELECT
				MIN(N4GPF2.N4_DATA) N4_DATA,
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR GRUPODE, NMGPF2.NM_GRUPO GRUPOPR, N4GPF2.N4_CONTA CONTADE, NMGPF2.NM_CCONTAB CONTAPR
				FROM %table:SNM% NMGPF2
				JOIN %table:SN4% N4GPF2 ON
						N4GPF2.D_E_L_E_T_= ' '
					AND N4GPF2.N4_FILIAL = NMGPF2.NM_FILIAL
					AND N4GPF2.N4_CBASE = NMGPF2.NM_CBASE
					AND N4GPF2.N4_ITEM = NMGPF2.NM_ITEM
					AND N4GPF2.N4_DATA = NMGPF2.NM_DTAPVRE
					AND N4GPF2.N4_OCORR = '03'
					AND N4GPF2.N4_TIPOCNT = '1'
					AND N4GPF2.N4_GRUPOTR <> NMGPF2.NM_GRUPO
					AND N4GPF2.N4_GRUPOTR <> ' '
				WHERE
						NMGPF2.D_E_L_E_T_= ' '
					AND NMGPF2.NM_MOTBX=' '
				GROUP BY
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR, NMGPF2.NM_GRUPO, N4GPF2.N4_CONTA, NMGPF2.NM_CCONTAB  ) GPFIN2 ON
				GPFIN2.N4_FILIAL = SN3.N3_FILIAL
			AND GPFIN2.N4_CBASE = SN3.N3_CBASE
			AND GPFIN2.N4_ITEM = SN3.N3_ITEM
			AND GPFIN2.N4_DATA > %exp:cLastDay%
		LEFT JOIN %table:SNG% SNG ON
				SNG.%NotDel%
		      AND NG_GRUPO = (CASE WHEN GPINI.GRUPODE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.GRUPODE
                           WHEN GPFIN1.GRUPODE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN1.GRUPODE
                           WHEN GPFIN2.GRUPODE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN2.GRUPODE
ELSE SN1.N1_GRUPO END)

		WHERE
				SN3.N3_CBASE BETWEEN ' ' AND 'ZZZ'
			AND SN1.N1_GRUPO BETWEEN ' ' AND 'ZZZ'
			AND SN3.N3_CCUSTO BETWEEN ' ' AND 'ZZZ'
			AND SN1.N1_LOCAL BETWEEN ' ' AND 'ZZZ'
			AND SN1.N1_CHAPA BETWEEN ' ' AND 'ZZZ'
			AND SN3.N3_TIPO = '01'
			AND SN3.%NotDel%
		GROUP BY
			SNG.NG_GRUPO,
			NG_DESCRIC,
CASE WHEN SN4.N4_DATA <= %exp:cLastDayBef% THEN '1' ELSE '2' END,
CASE WHEN GPINI.CONTADE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.CONTADE
                           WHEN GPFIN1.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN1.CONTADE
                           WHEN GPFIN2.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB END,
			N3_CDEPREC
		UNION ALL
		SELECT '3',
			SN1.N1_GRUPO,
			SNG.NG_DESCRIC,
			SUM(SN3.N3_VORIG1) VORIG,
			SUM(SN3.N3_VRDACM1) DPRACUM,
			0 SLDINIDEPR,
			0 AMPLIA,
			SN3.N3_CCONTAB,
			SN3.N3_CDEPREC
		FROM %table:SN1% SN1
		INNER JOIN %table:SN3% SN3 ON
				SN1.N1_FILIAL = SN3.N3_FILIAL
			AND SN1.N1_CBASE = SN3.N3_CBASE
			AND SN1.N1_ITEM = SN3.N3_ITEM
			AND SN1.%NotDel%
		INNER JOIN %table:SN1% SN1 ON
				SN1.%NotDel%
			AND SN1.N1_FILIAL = SN3.N3_FILIAL
			AND SN1.N1_CBASE = SN3.N3_CBASE
			AND SN1.N1_ITEM = SN3.N3_ITEM
		INNER JOIN %table:SNG% SNG ON
				SNG.%NotDel%
			AND SNG.NG_GRUPO = SN1.N1_GRUPO
		LEFT JOIN %table:SN4% SN4 ON
				SN4.N4_FILIAL = SN3.N3_FILIAL
			AND SN4.N4_CBASE = SN3.N3_CBASE
			AND SN4.N4_ITEM = SN3.N3_ITEM
			AND SN4.N4_TIPO = SN3.N3_TIPO
			AND SN4.N4_DATA = SN3.N3_DTBAIXA
			AND SN4.N4_SEQ = SN3.N3_SEQ
			AND SN4.N4_OCORR = '01'
			AND SN4.N4_TIPOCNT = '1'
			AND SN4.%NotDel%
		WHERE
				SN3.N3_DTBAIXA BETWEEN %exp:cFirstDay% AND %exp:cLastDay%
			AND SN3.N3_BAIXA <> '0'
			AND SN3.N3_CCONTAB <> ' '
			AND SN3.%NotDel%
			AND SN4.N4_MOTIVO <> '18'
		GROUP BY '3',
			SN1.N1_GRUPO,
			SNG.NG_DESCRIC,
			SN3.N3_CCONTAB,
			SN3.N3_CDEPREC
		UNION ALL
		SELECT '4',
			SNG.NG_GRUPO,
			SNG.NG_DESCRIC,
			SUM(SN4.N4_VLROC1) VORIG,
			0 DPRACUM,
			0 SLDINIDEPR,
			0 AMPLIA,
CASE WHEN GPINI.CONTADE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.CONTADE
                           WHEN GPFIN1.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN1.CONTADE
                           WHEN GPFIN2.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB END N3_CCONTAB,
			SN3.N3_CDEPREC
		FROM %table:SN4% SN4
		JOIN %table:SN1% SN1 ON
				N1_FILIAL = N4_FILIAL
			AND N1_CBASE = N4_CBASE
			AND N1_ITEM = N4_ITEM
			AND SN1.D_E_L_E_T_= ' '
		JOIN %table:SN3% SN3 ON
				N3_FILIAL = N4_FILIAL
			AND N3_CBASE = N4_CBASE
			AND N3_ITEM = N4_ITEM
			AND N3_TIPO = N4_TIPO
			AND N3_SEQ = N4_SEQ
			AND SN3.D_E_L_E_T_= ' '
		// TRANF GROPO INICIO DO PERIODO
		LEFT JOIN
				(SELECT
				MIN(N4GPI.N4_DATA) N4_DATA,
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR GRUPODE, SNMGPI.NM_GRUPO GRUPOPR, N4GPI.N4_CONTA CONTADE, SNMGPI.NM_CCONTAB CONTAPR
				FROM %table:SNM% SNMGPI
				JOIN %table:SN4% N4GPI ON
						N4GPI.D_E_L_E_T_= ' '
					AND N4GPI.N4_FILIAL = SNMGPI.NM_FILIAL
					AND N4GPI.N4_CBASE = SNMGPI.NM_CBASE
					AND N4GPI.N4_ITEM = SNMGPI.NM_ITEM
					AND N4GPI.N4_DATA = SNMGPI.NM_DTAPVRE
					AND N4GPI.N4_OCORR = '03'
					AND N4GPI.N4_TIPOCNT = '1'
					AND N4GPI.N4_GRUPOTR <> SNMGPI.NM_GRUPO
					AND N4GPI.N4_GRUPOTR <> ' '
				WHERE
						SNMGPI.D_E_L_E_T_= ' '
					AND SNMGPI.NM_MOTBX=' '
				GROUP BY
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR, SNMGPI.NM_GRUPO, N4GPI.N4_CONTA, SNMGPI.NM_CCONTAB  ) GPINI ON
				GPINI.N4_FILIAL = SN3.N3_FILIAL
			AND GPINI.N4_CBASE = SN3.N3_CBASE
			AND GPINI.N4_ITEM = SN3.N3_ITEM
			AND GPINI.N4_DATA >= %exp:cFirstDay%
		// TRANF GROPO FINAL DO PERIODO
		LEFT JOIN
				(SELECT
				MAX(N4GPF1.N4_DATA) N4_DATA,
				N4GPF1.N4_FILIAL, N4GPF1.N4_CBASE, N4GPF1.N4_ITEM, N4GPF1.N4_TIPO, N4GPF1.N4_GRUPOTR GRUPODE, SNMGPF1.NM_GRUPO GRUPOPR, N4GPF1.N4_CONTA CONTADE, SNMGPF1.NM_CCONTAB CONTAPR
				FROM %table:SNM% SNMGPF1
				JOIN %table:SN4% N4GPF1 ON
						N4GPF1.D_E_L_E_T_= ' '
					AND N4GPF1.N4_FILIAL = SNMGPF1.NM_FILIAL
					AND N4GPF1.N4_CBASE = SNMGPF1.NM_CBASE
					AND N4GPF1.N4_ITEM = SNMGPF1.NM_ITEM
					AND N4GPF1.N4_DATA = SNMGPF1.NM_DTAPVRE
					AND N4GPF1.N4_OCORR = '03'
					AND N4GPF1.N4_TIPOCNT = '1'
					AND N4GPF1.N4_GRUPOTR <> SNMGPF1.NM_GRUPO
					AND N4GPF1.N4_GRUPOTR <> ' '
				WHERE
						SNMGPF1.D_E_L_E_T_= ' '
					AND SNMGPF1.NM_MOTBX=' '
				GROUP BY
				N4GPF1.N4_FILIAL, N4GPF1.N4_CBASE, N4GPF1.N4_ITEM, N4GPF1.N4_TIPO, N4GPF1.N4_GRUPOTR, SNMGPF1.NM_GRUPO, N4GPF1.N4_CONTA, SNMGPF1.NM_CCONTAB  ) GPFIN1 ON
				GPFIN1.N4_FILIAL = SN3.N3_FILIAL
			AND GPFIN1.N4_CBASE = SN3.N3_CBASE
			AND GPFIN1.N4_ITEM = SN3.N3_ITEM
			AND GPFIN1.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay%
		// TRANF GROPO INICAL DO PERIODO SEQUINTE
		LEFT JOIN
			(SELECT
				MIN(N4GPF2.N4_DATA) N4_DATA,
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR GRUPODE, NMGPF2.NM_GRUPO GRUPOPR, N4GPF2.N4_CONTA CONTADE, NMGPF2.NM_CCONTAB CONTAPR
				FROM %table:SNM% NMGPF2
				JOIN %table:SN4% N4GPF2 ON
						N4GPF2.D_E_L_E_T_= ' '
					AND N4GPF2.N4_FILIAL = NMGPF2.NM_FILIAL
					AND N4GPF2.N4_CBASE = NMGPF2.NM_CBASE
					AND N4GPF2.N4_ITEM = NMGPF2.NM_ITEM
					AND N4GPF2.N4_DATA = NMGPF2.NM_DTAPVRE
					AND N4GPF2.N4_OCORR = '03'
					AND N4GPF2.N4_TIPOCNT = '1'
					AND N4GPF2.N4_GRUPOTR <> NMGPF2.NM_GRUPO
					AND N4GPF2.N4_GRUPOTR <> ' '
				WHERE
						NMGPF2.D_E_L_E_T_= ' '
					AND NMGPF2.NM_MOTBX=' '
				GROUP BY
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR, NMGPF2.NM_GRUPO, N4GPF2.N4_CONTA, NMGPF2.NM_CCONTAB  ) GPFIN2 ON
				GPFIN2.N4_FILIAL = SN3.N3_FILIAL
			AND GPFIN2.N4_CBASE = SN3.N3_CBASE
			AND GPFIN2.N4_ITEM = SN3.N3_ITEM
			AND GPFIN2.N4_DATA > %exp:cLastDay%
		LEFT JOIN %table:SNG% SNG
		ON SNG.%NotDel%
			AND NG_GRUPO = (CASE WHEN GPINI.GRUPODE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.GRUPODE
						WHEN GPFIN1.GRUPODE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN1.GRUPODE
						WHEN GPFIN2.GRUPODE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN2.GRUPODE
ELSE SN1.N1_GRUPO END)
		WHERE
				SN4.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay%
			AND ( (SN4.N4_OCORR IN ('03') AND SN4.N4_FILIAL <> ' ') OR (SN4.N4_OCORR IN ('04') AND SN4.N4_FILORIG <> ' ') )
			AND SN4.N4_TIPOCNT IN ('1')
			AND SN4.%NotDel%
		GROUP BY '4',
			SNG.NG_GRUPO,
			SNG.NG_DESCRIC,
CASE WHEN GPINI.CONTADE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.CONTADE
                           WHEN GPFIN1.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN1.CONTADE
                           WHEN GPFIN2.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('03','05','06') THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB END,
			SN3.N3_CDEPREC
		UNION ALL
		SELECT '5',
			SNG.NG_GRUPO N1_GRUPO,
			SNG.NG_DESCRIC,
			SUM(SN3.N3_VORIG1+(CASE WHEN GPINIA.N4_VLROC1 IS NOT NULL THEN GPINIA.N4_VLROC1 ELSE 0 END)) VORIG,
			SUM(SN3.N3_VRDACM1) DPRACUM,
			0 SLDINIDEPR,
			0 AMPLIA,
CASE
				WHEN GPINI.CONTADE IS NOT NULL AND SN3.N3_AQUISIC < %exp:cFirstDay% THEN GPINI.CONTADE
				WHEN GPFIN2.CONTADE IS NOT NULL AND SN3.N3_AQUISIC >= %exp:cFirstDay% THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB
END N3_CCONTAB,
			SN3.N3_CDEPREC
		FROM %table:SN3% SN3
		LEFT JOIN %table:SN1% SN1 ON
				SN1.N1_FILIAL = SN3.N3_FILIAL
			AND SN1.N1_CBASE = SN3.N3_CBASE
			AND SN1.N1_ITEM = SN3.N3_ITEM
			AND SN1.D_E_L_E_T_ <> '*'
		// TRANF AMPLIAÇÃO DO MES
		LEFT JOIN
			(SELECT N4GPIA.N4_FILIAL,
					N4GPIA.N4_CBASE,
					N4GPIA.N4_ITEM,
					N4GPIA.N4_DATA,
					SN1GPIA.N1_GRUPO GRUPODE,
					SUM(N4GPIA.N4_VLROC1) N4_VLROC1
			FROM %table:SN1% SN1GPIA
			JOIN %table:SN4% N4GPIA
			ON N4GPIA.D_E_L_E_T_= ' '
			AND N4GPIA.N4_FILIAL = SN1GPIA.N1_FILIAL
			AND N4GPIA.N4_CBASE = SN1GPIA.N1_CBASE
			AND N4GPIA.N4_ITEM = SN1GPIA.N1_ITEM
			AND N4GPIA.N4_OCORR = '09'
			AND N4GPIA.N4_TIPOCNT = '1'
			WHERE SN1GPIA.D_E_L_E_T_= ' '
			GROUP BY N4GPIA.N4_FILIAL,
					N4GPIA.N4_CBASE,
					N4GPIA.N4_ITEM,
					N4GPIA.N4_DATA,
					SN1GPIA.N1_GRUPO) GPINIA ON
			GPINIA.N4_FILIAL = SN3.N3_FILIAL
		AND GPINIA.N4_CBASE = SN3.N3_CBASE
		AND GPINIA.N4_ITEM = SN3.N3_ITEM
		AND GPINIA.N4_DATA >= %exp:cFirstDay%
		AND GPINIA.N4_DATA <= %exp:cLastDay%
		// TRANF GROPO INICIO DO PERIODO
		LEFT JOIN
				(SELECT
				MIN(N4GPI.N4_DATA) N4_DATA,
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR GRUPODE, SNMGPI.NM_GRUPO GRUPOPR, N4GPI.N4_CONTA CONTADE, SNMGPI.NM_CCONTAB CONTAPR
				FROM %table:SNM% SNMGPI
				JOIN %table:SN4% N4GPI ON
						N4GPI.D_E_L_E_T_= ' '
					AND N4GPI.N4_FILIAL = SNMGPI.NM_FILIAL
					AND N4GPI.N4_CBASE = SNMGPI.NM_CBASE
					AND N4GPI.N4_ITEM = SNMGPI.NM_ITEM
					AND N4GPI.N4_DATA = SNMGPI.NM_DTAPVRE
					AND N4GPI.N4_OCORR = '03'
					AND N4GPI.N4_TIPOCNT = '1'
					AND N4GPI.N4_GRUPOTR <> SNMGPI.NM_GRUPO
					AND N4GPI.N4_GRUPOTR <> ' '
				WHERE
						SNMGPI.D_E_L_E_T_= ' '
					AND SNMGPI.NM_MOTBX=' '
				GROUP BY
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR, SNMGPI.NM_GRUPO, N4GPI.N4_CONTA, SNMGPI.NM_CCONTAB  ) GPINI ON
				GPINI.N4_FILIAL = SN3.N3_FILIAL
			AND GPINI.N4_CBASE = SN3.N3_CBASE
			AND GPINI.N4_ITEM = SN3.N3_ITEM
			AND GPINI.N4_DATA >= %exp:cFirstDay%
		// TRANF GROPO INICAL DO PERIODO SEQUINTE
		LEFT JOIN
			(SELECT
				MIN(N4GPF2.N4_DATA) N4_DATA,
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR GRUPODE, NMGPF2.NM_GRUPO GRUPOPR, N4GPF2.N4_CONTA CONTADE, NMGPF2.NM_CCONTAB CONTAPR
				FROM %table:SNM% NMGPF2
				JOIN %table:SN4% N4GPF2 ON
						N4GPF2.D_E_L_E_T_= ' '
					AND N4GPF2.N4_FILIAL = NMGPF2.NM_FILIAL
					AND N4GPF2.N4_CBASE = NMGPF2.NM_CBASE
					AND N4GPF2.N4_ITEM = NMGPF2.NM_ITEM
					AND N4GPF2.N4_DATA = NMGPF2.NM_DTAPVRE
					AND N4GPF2.N4_OCORR = '03'
					AND N4GPF2.N4_TIPOCNT = '1'
					AND N4GPF2.N4_GRUPOTR <> NMGPF2.NM_GRUPO
					AND N4GPF2.N4_GRUPOTR <> ' '
				WHERE
						NMGPF2.D_E_L_E_T_= ' '
					AND NMGPF2.NM_MOTBX=' '
				GROUP BY
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR, NMGPF2.NM_GRUPO, N4GPF2.N4_CONTA, NMGPF2.NM_CCONTAB  ) GPFIN2 ON
				GPFIN2.N4_FILIAL = SN3.N3_FILIAL
			AND GPFIN2.N4_CBASE = SN3.N3_CBASE
			AND GPFIN2.N4_ITEM = SN3.N3_ITEM
			AND GPFIN2.N4_DATA > %exp:cLastDay%
		LEFT JOIN %table:SNG% SNG
		ON SNG.%NotDel%
				AND NG_GRUPO = (CASE
                          		WHEN GPINIA.GRUPODE IS NOT NULL AND SN3.N3_AQUISIC < %exp:cFirstDay% THEN GPINIA.GRUPODE
								WHEN GPINI.GRUPODE IS NOT NULL AND SN3.N3_AQUISIC < %exp:cFirstDay% THEN GPINI.GRUPODE
								WHEN GPFIN2.GRUPODE IS NOT NULL AND SN3.N3_AQUISIC >= %exp:cFirstDay% THEN GPFIN2.GRUPODE
ELSE SN1.N1_GRUPO END)
		WHERE
				SN3.N3_CCONTAB <= 'ZZZZZZZZZZZZZZZ' AND SN3.N3_CCUSTO <= 'ZZZZZZZZZ'
			AND SN3.N3_AQUISIC BETWEEN %exp:cFirstDay% AND %exp:cLastDay%
			AND SN3.N3_TIPO IN ('01', '10', '12')
			AND SN3.N3_BAIXA = '0'
			AND SN3.%NotDel%
		GROUP BY '5',
			SNG.NG_GRUPO ,
			SNG.NG_DESCRIC,
CASE
				WHEN GPINI.CONTADE IS NOT NULL AND SN3.N3_AQUISIC < %exp:cFirstDay% THEN GPINI.CONTADE
				WHEN GPFIN2.CONTADE IS NOT NULL AND SN3.N3_AQUISIC >= %exp:cFirstDay% THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB
END,
			SN3.N3_CDEPREC
		UNION ALL
		SELECT '6'
			,SNG.NG_GRUPO
			,SNG.NG_DESCRIC
			,0 VORIG
			,SUM(SN4.N4_VLROC1)
			,0 DPRACUM
			,0 SLDINIDEPR
            ,CASE WHEN GPINI.CONTADE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.CONTADE
                           WHEN GPFIN2.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('06') THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB END N3_CCONTAB
			,N3_CDEPREC
		FROM %table:SN4% SN4
		INNER JOIN %table:SN1% SN1 ON
				SN1.N1_FILIAL = SN4.N4_FILIAL
			AND SN1.N1_CBASE = SN4.N4_CBASE
			AND SN1.N1_ITEM = SN4.N4_ITEM
			AND SN1.%NotDel%
			AND (N1_DTBLOQ = ' ' OR N1_DTBLOQ >= %exp:cFirstDay% )
			AND (N1_BAIXA = ' ' OR N1_BAIXA >= %exp:cFirstDay% )
		INNER JOIN %table:SN3% SN3 ON
				N3_FILIAL = N4_FILIAL
			AND N3_CBASE = N4_CBASE
			AND N3_ITEM = N4_ITEM
			AND N3_TIPO = N4_TIPO
			AND N3_SEQ = N4_SEQ
			AND SN3.%NotDel%
		// TRANF GROPO INICIO DO PERIODO
		LEFT JOIN
				(SELECT
				MIN(N4GPI.N4_DATA) N4_DATA,
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR GRUPODE, SNMGPI.NM_GRUPO GRUPOPR, N4GPI.N4_CONTA CONTADE, SNMGPI.NM_CCONTAB CONTAPR
				FROM %table:SNM% SNMGPI
				JOIN %table:SN4% N4GPI ON
						N4GPI.D_E_L_E_T_= ' '
					AND N4GPI.N4_FILIAL = SNMGPI.NM_FILIAL
					AND N4GPI.N4_CBASE = SNMGPI.NM_CBASE
					AND N4GPI.N4_ITEM = SNMGPI.NM_ITEM
					AND N4GPI.N4_DATA = SNMGPI.NM_DTAPVRE
					AND N4GPI.N4_OCORR = '03'
					AND N4GPI.N4_TIPOCNT = '1'
					AND N4GPI.N4_GRUPOTR <> SNMGPI.NM_GRUPO
					AND N4GPI.N4_GRUPOTR <> ' '
				WHERE
						SNMGPI.D_E_L_E_T_= ' '
					AND SNMGPI.NM_MOTBX=' '
				GROUP BY
				N4GPI.N4_FILIAL, N4GPI.N4_CBASE, N4GPI.N4_ITEM, N4GPI.N4_TIPO, N4GPI.N4_GRUPOTR, SNMGPI.NM_GRUPO, N4GPI.N4_CONTA, SNMGPI.NM_CCONTAB  ) GPINI ON
				GPINI.N4_FILIAL = SN3.N3_FILIAL
			AND GPINI.N4_CBASE = SN3.N3_CBASE
			AND GPINI.N4_ITEM = SN3.N3_ITEM
			AND GPINI.N4_DATA >= %exp:cFirstDay%
		LEFT JOIN
			(SELECT
				MIN(N4GPF2.N4_DATA) N4_DATA,
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR GRUPODE, NMGPF2.NM_GRUPO GRUPOPR, N4GPF2.N4_CONTA CONTADE, NMGPF2.NM_CCONTAB CONTAPR
				FROM %table:SNM% NMGPF2
				JOIN %table:SN4% N4GPF2 ON
						N4GPF2.D_E_L_E_T_= ' '
					AND N4GPF2.N4_FILIAL = NMGPF2.NM_FILIAL
					AND N4GPF2.N4_CBASE = NMGPF2.NM_CBASE
					AND N4GPF2.N4_ITEM = NMGPF2.NM_ITEM
					AND N4GPF2.N4_DATA = NMGPF2.NM_DTAPVRE
					AND N4GPF2.N4_OCORR = '03'
					AND N4GPF2.N4_TIPOCNT = '1'
					AND N4GPF2.N4_GRUPOTR <> NMGPF2.NM_GRUPO
					AND N4GPF2.N4_GRUPOTR <> ' '
				WHERE
						NMGPF2.D_E_L_E_T_= ' '
					AND NMGPF2.NM_MOTBX=' '
				GROUP BY
				N4GPF2.N4_FILIAL, N4GPF2.N4_CBASE, N4GPF2.N4_ITEM, N4GPF2.N4_TIPO, N4GPF2.N4_GRUPOTR, NMGPF2.NM_GRUPO, N4GPF2.N4_CONTA, NMGPF2.NM_CCONTAB  ) GPFIN2 ON
				GPFIN2.N4_FILIAL = SN3.N3_FILIAL
			AND GPFIN2.N4_CBASE = SN3.N3_CBASE
			AND GPFIN2.N4_ITEM = SN3.N3_ITEM
			AND GPFIN2.N4_DATA > %exp:cLastDay%
		LEFT JOIN %table:SNG% SNG
		ON SNG.%NotDel%
		      AND NG_GRUPO = (CASE WHEN GPINI.GRUPODE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.GRUPODE
                           WHEN GPFIN2.GRUPODE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('06') THEN GPFIN2.GRUPODE
ELSE SN1.N1_GRUPO END)
		WHERE SN4.%NotDel%
		AND (SN4.N4_OCORR = '06' AND SN4.N4_TIPOCNT = '4')
		AND SN4.N4_DATA BETWEEN %exp:cFirstDay% AND %exp:cLastDay%
		GROUP BY '6'
		,NG_GRUPO
		,NG_DESCRIC
        ,CASE WHEN GPINI.CONTADE IS NOT NULL AND SN4.N4_DATA < %exp:cFirstDay% THEN GPINI.CONTADE
                           WHEN GPFIN2.CONTADE  IS NOT NULL AND SN4.N4_DATA >= %exp:cFirstDay% AND SN4.N4_OCORR IN ('06') THEN GPFIN2.CONTADE
ELSE SN3.N3_CCONTAB END
		,N3_CDEPREC
		)tb
	)td
GROUP BY NG_DESCRIC,
	N3_CCONTAB,
	N3_CDEPREC
ORDER BY 1

EndSql

(TMP)->(DbEval({||  AADD(aDados, {;
		NG_DESCRIC,;
		N3_CCONTAB,;
		N3_CDEPREC,;
		SI_ATIVO,;
		AQ_ATIVO,;
		MV_ATIVO,;//MV_ATIVO,;
		TF_ATIVO,;
		BX_ATIVO,;
		SI_DEPREC,;
		AQ_DEPREC,;
		MV_DEPREC,;//MV_DEPREC,;
		TF_DEPREC,;
		BX_DEPREC;
		})}))



	(TMP)->(DbCloseArea())


Return aDados

//------------------------------------------------------------------------------------------
/*/{Protheus.doc} ValidPerg
Ajustar Grupo de perguntas ou criá-lo caso não exista
@author    Eduardo Santana
@version   1.00
@since     16/12/2016
/*/
//------------------------------------------------------------------------------------------
Static Function ValidPerg(cPerg)

aHelpPor := u_xFixHelp("Digite o mês e ano de referencia no formato MM/AAAA")

PutSx1(cPerg,"01","Mes/ano referencia ?","","","MV_CH1","C", 7,0,0,"G","","","","","MV_PAR01",""  ,""   ,""  ,"" ,"","","","","","","","","","","","",aHelpPor,{},{},"")

aHelpPor := {}

Return Pergunte(cPerg,.T.)

